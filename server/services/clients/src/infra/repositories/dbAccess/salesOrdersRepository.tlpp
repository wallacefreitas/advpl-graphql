#include "totvs.ch"

namespace project.advplgraphql

/*/{Protheus.doc} DBAccessClientsRepository

Repositório da entidade Pedido de Venda utilzando o dbAccess
	
@type class
@author Wallace de Freitas
@since 09/03/2024
@version P11,P12

/*/
class DBAccessSalesOrdersRepository
    public method new()                                 constructor
    public method findAllPerClient(cCGC as character)   as json

endClass

Method new() class DBAccessSalesOrdersRepository

Return

Method findAllPerClient(nClientId as integer) as json class DBAccessSalesOrdersRepository
    Local jSaleOrders   := JsonObject():new()   as json
    Local aJson         := {}                   as array
    Local aItems        := {}                   as array
    Local nLastPos      := 0                    as integer
    Local nLastItemPos  := 0                    as integer
    Default nClientId   := 0

    conout(nClientId)
    dbSelectArea("SA1")
    SA1->(dbGoTo(nClientId))

    dbSelectArea("SC5")
    SC5->(dbSetOrder(3))

    If SC5->(msSeek(FWxFilial("SC5") + SA1->A1_COD + SA1->A1_LOJA))
        dbSelectArea("SC6")
        SC6->(dbSetOrder(1))

        While !SC5->(EoF()) .and. (SC5->C5_CLIENT == SA1->A1_COD .and. SC5->C5_LOJACLI == SA1->A1_LOJA)
            aAdd(aJson, JsonObject():new())

            aItems                  := {}
            nLastPos                := len(aJson)
            aJson[nLastPos]['code'] := SC5->C5_NUM

            If SC6->(msSeek(FWxFilial("SC6") + SC5->C5_NUM))
                While !SC6->(EoF()) .and. (SC6->C6_FILIAL == SC5->C5_FILIAL .and. SC6->C6_NUM == SC5->C5_NUM)
                    aAdd(aItems, JsonObject():new())

                    nLastItemPos                    := len(aItems)
                    aItems[nLastItemPos]['item']    := SC6->C6_ITEM
                    aItems[nLastItemPos]['product'] := SC6->C6_PRODUTO

                    aJson[Len(aJson)]['items']      := aItems 

                    SC6->(dbSkip())
                EndDo               
            EndIf

            SC5->(dbSkip())
        EndDo

        SC6->(dbCloseArea())
    EndIf

    jSaleOrders:set(aJson)

    SA1->(dbCloseArea())
    SC5->(dbCloseArea())

Return jSaleOrders
